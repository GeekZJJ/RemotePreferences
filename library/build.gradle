plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 1
        targetSdkVersion 30
        versionCode 8
        versionName '0.8'
    }
}

task sourceJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'com.crossbowffs.remotepreferences'
                artifactId = 'remotepreferences'
                version = android.defaultConfig.versionName

                artifact sourceJar
                artifact javadocJar

                pom {
                    packaging = 'aar'
                    name = 'RemotePreferences'
                    description = 'A drop-in solution for inter-app access to SharedPreferences on Android.'
                    url = 'https://github.com/apsun/RemotePreferences'
                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            name = 'Andrew Sun'
                            email = 'andrew@crossbowffs.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/apsun/RemotePreferences.git'
                        developerConnection = 'scm:git:https://github.com/apsun/RemotePreferences.git'
                        url = 'https://github.com/apsun/RemotePreferences'
                    }
                }
            }
        }
    }

    signing {
        useGpgCmd()
        sign publishing.publications.release
    }
}