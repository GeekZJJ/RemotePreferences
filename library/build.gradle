apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

ext {
    packageName = 'com.crossbowffs.remotepreferences'
    libraryName = 'remotepreferences'
    versionCode = 7
    versionName = '0.7'
    licenses = ['MIT']
    vcsUrl = 'https://github.com/apsun/RemotePreferences.git'
}

group = project.packageName
version = project.versionName

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 1
        targetSdkVersion 30
        versionCode project.versionCode
        versionName project.versionName
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = project.libraryName
        licenses = project.licenses
        vcsUrl = project.vcsUrl
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            artifactId project.libraryName
        }
    }
}
