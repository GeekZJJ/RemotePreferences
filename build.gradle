buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:2.1.3"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6"
        classpath "com.github.dcendents:android-maven-gradle-plugin:1.3"
    }
}

apply plugin: "com.android.library"
apply plugin: "com.jfrog.bintray"
apply plugin: "com.github.dcendents.android-maven"

group = "com.crossbowffs.remotepreferences"
version = "0.3"

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    defaultConfig {
        minSdkVersion 1
        targetSdkVersion 24
        versionCode 3
        versionName "0.3"
    }

    sourceSets {
        main {
            manifest.srcFile "AndroidManifest.xml"
            java.srcDir "src"
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = "sources"
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_API_KEY")
    pkg {
        repo = "maven"
        name = "remotepreferences"
        licenses = ["MIT"]
        vcsUrl = "https://github.com/apsun/RemotePreferences.git"
        configurations = ["archives"]
        version {
            name = "0.3"
        }
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            name "remotepreferences"
            description "A drop-in solution for inter-app access to SharedPreferences on Android."
            url "https://github.com/apsun/RemotePreferences"
            inceptionYear "2016"

            packaging "aar"
            groupId "com.crossbowffs.remotepreferences"
            artifactId "remotepreferences"
            version "0.3"

            licenses {
                license {
                    name "MIT"
                    url "https://opensource.org/licenses/MIT"
                }
            }
            scm {
                connection "https://github.com/apsun/RemotePreferences.git"
                url "https://github.com/apsun/RemotePreferences"
            }
            developers {
                developer {
                    id "crossbowffs"
                    name "Andrew Sun"
                    email "0xandrewsun@gmail.com"
                }
            }
        }
    }
}
